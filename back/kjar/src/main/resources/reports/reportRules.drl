template header
attackType
num

package reports;

import rs.sbnz.model.events.FailedLoginEvent;
import rs.sbnz.model.events.BlockEvent;
import rs.sbnz.model.events.AttackEvent;
import rs.sbnz.model.AttackType;

query reportAttackEventOver24h(AttackEvent e1, int num)
    Number(intValue >= num) from accumulate(
        $e2: AttackEvent(
            this != e1,
            type == e1.type
        ) over window:time(24h),
        count($e2)
    )
end

query reportAnyAttackEventOver24h(AttackEvent e1, int num)
    Number(intValue >= num) from accumulate(
        $e2: AttackEvent(
            this != e1
        ) over window:time(24h),
        count($e2)
    )
end

query reportFailedLoginEventOver24h(FailedLoginEvent e1, int num)
    Number(intValue >= num) from accumulate(
        $e2: FailedLoginEvent(
            this != e1,
            ip == e1.ip
        ) over window:time(24h),
        count($e2)
    )
end

query reportBlockEventOver24h(BlockEvent e1, int num)
    Number(intValue >= num) from accumulate(
        $e2: BlockEvent(
            this != e1,
            ip == e1.ip
        ) over window:time(24h),
        count($e2)
    )
end

template "reports"

rule "Show attack events of type @{attackType} in last 24 hours if there is more than ${num} of them"
    agenda-group "reports"
    when
        $e1: AttackEvent(type == @{attackType}) over window:time(24h)
        reportAttackEventOver24h($e1, @{num}-1;)
    then
        // Note that we don't print the timestamp in the actual rule, but if need be can retrieve it through
        // insert's fact handle's getStartTimestamp()
        // https://lists.jboss.org/archives/list/rules-users@lists.jboss.org/thread/FOYSWWYQ6H3IQ22I7YWBGJQSMJWM6NEE/
        System.out.println($e1);
end

rule "Show attack events of all types in last 24 hours if there is more than @{num} of them"
    agenda-group "reports2"
    when
        $e1: AttackEvent() over window:time(24h)
        reportAnyAttackEventOver24h($e1, @{num}-1;)
    then
        System.out.println($e1);
end

rule "Show failed login attempts from same ip address in last 24 hours if there is more than @{num} of them"
    agenda-group "reports"
    when
        $e1: FailedLoginEvent($ip: ip) over window:time(24h)
        reportFailedLoginEventOver24h($e1, @{num}-1;)
    then
        System.out.println($e1);
end

rule "Show block events for same ip address in last 24 hours if there is more than @{num} of them"
    agenda-group "reports"
    when
        $e1: BlockEvent($ip: ip) over window:time(24h)
        reportBlockEventOver24h($e1, @{num}-1;)
    then
        System.out.println($e1);
end

end template
